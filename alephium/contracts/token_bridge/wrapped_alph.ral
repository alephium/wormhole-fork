import "wrapped_alph_pool.ral"

Contract WrappedAlph(
    mut totalWrapped: U256
) {
    @using(preapprovedAssets=true, assetsInContract=true, permissionCheck=false)
    pub fn wrapAndTransfer(
        fromAddress: Address,
        totalAmount: U256
    ) -> () {
        totalWrapped = totalWrapped + totalAmount
        transferAlphToSelf!(fromAddress, totalAmount)
        let self = selfAddress!()
        WrappedAlphPool(callerContractId!()).receiveWrappedAlph{
            self -> selfTokenId!(): totalAmount
        }(self, totalAmount)
    }

    @using(preapprovedAssets=true, assetsInContract=true, permissionCheck=false)
    pub fn unwrapAndCompleteTransfer(
        fromAddress: Address,
        recipient: Address,
        transferAmount: U256,
        arbiter: Address,
        arbiterFee: U256
    ) -> () {
        let totalAmount = transferAmount + arbiterFee
        totalWrapped = totalWrapped - totalAmount

        transferTokenToSelf!(fromAddress, selfTokenId!(), totalAmount)
        transferAlphFromSelf!(recipient, transferAmount)
        if (arbiterFee > 0) {
            transferAlphFromSelf!(arbiter, arbiterFee)
        }
    }
}
