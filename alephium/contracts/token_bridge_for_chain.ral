import "governance.ral"
import "token_interface.ral"
import "math.ral"
import "sequence.ral"

TxContract TokenBridgeForChain(
    governanceId: ByteVec,
    localChainId: U256,
    localTokenBridgeId: ByteVec, // local chain token bridge contract id
    remoteChainId: U256,
    remoteTokenBridgeId: ByteVec, // remote chain token bridge contract id
    mut next: U256,
    mut next1: U256,
    mut next2: U256,
    mut undoneSequenceId: ByteVec,
    tokenWrapperTemplateId: ByteVec, // used for create contract by copyCreateContract
    tokenWrapperCodeHash: ByteVec,
    undoneSequenceCodeHash: ByteVec,
    eventEmitterId: ByteVec
) extends Sequence(next, next1, next2, undoneSequenceId, undoneSequenceCodeHash, eventEmitterId) {
    pub fn getLocalTokenBridgeId() -> (ByteVec) {
        return localTokenBridgeId
    }

    pub fn getLocalChainId() -> (U256) {
        return localChainId
    }

    // return payload
    fn verifyVAA(vaa: ByteVec) -> (ByteVec) {
        let governance = Governance(governanceId)
        let (emitterChainId, emitterAddress, msgSequence, payload) = governance.parseAndVerifyVAA(vaa)
        assert!(emitterChainId == remoteChainId)
        assert!(emitterAddress == remoteTokenBridgeId)
        checkSequence(msgSequence)
        return payload
    }

    // return (tokenContractId, tokenDecimals, tokenSymbol, tokenName)
    fn parseAttestToken(vaa: ByteVec) -> (ByteVec, U256, ByteVec, ByteVec) {
        let payload = verifyVAA(vaa)
        assert!(byteVecSlice!(payload, 0, 1) == #02) // payload id
        let tokenContractId = byteVecSlice!(payload, 1, 33)
        let tokenChainId = u256From2Byte!(byteVecSlice!(payload, 33, 35))
        assert!(tokenChainId == remoteChainId)
        let decimals = u256From1Byte!(byteVecSlice!(payload, 35, 36))
        let symbol = byteVecSlice!(payload, 36, 68)
        let name = byteVecSlice!(payload, 68, 100)
        assert!(size!(payload) == 100)
        return tokenContractId, decimals, symbol, name
    }

    pub payable fn createWrapperForLocalToken(tokenId: ByteVec, payer: Address, createContractAlphAmount: U256) -> () {
        let token = ERC20Token(tokenId)
        let symbol = token.symbol()
        let symbolSize = size!(symbol)
        assert!(symbolSize <= 32)
        let name = token.name()
        let nameSize = size!(name)
        assert!(nameSize <= 32)

        approveAlph!(payer, createContractAlphAmount)
        let encodedState = encodeToByteVec!(
            localTokenBridgeId,
            selfContractId!(),
            localChainId,
            remoteChainId,
            tokenId,
            true,
            zeros!(32 - symbolSize) ++ symbol,
            zeros!(32 - nameSize) ++ name,
            token.decimals()
        )
        let contractId = copyCreateContract!(tokenWrapperTemplateId, encodedState)
        let eventEmitter = EventEmitter(eventEmitterId)
        eventEmitter.publishTokenWrapperCreated(contractId, true, tokenId, remoteChainId)
    }

    pub payable fn createWrapperForRemoteToken(vaa: ByteVec, payer: Address, createContractAlphAmount: U256) -> () {
        let (tokenId, decimals, symbol, name) = parseAttestToken(vaa)
        approveAlph!(payer, createContractAlphAmount)
        let encodedState = encodeToByteVec!(
            localTokenBridgeId,
            selfContractId!(),
            localChainId,
            remoteChainId,
            tokenId,
            false,
            symbol,
            name,
            decimals
        )
        let tokenAmount = 0xffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff
        let contractId = copyCreateContractWithToken!(tokenWrapperTemplateId, encodedState, tokenAmount)
        let eventEmitter = EventEmitter(eventEmitterId)
        eventEmitter.publishTokenWrapperCreated(contractId, false, tokenId, remoteChainId)
    }

    pub fn parseTransfer(vaa: ByteVec) -> (ByteVec, U256, ByteVec, U256, U256) {
        assert!(callerCodeHash!() == tokenWrapperCodeHash)
        let payload = verifyVAA(vaa)
        assert!(byteVecSlice!(payload, 0, 1) == #01) // payloadId
        let amount = u256From32Byte!(byteVecSlice!(payload, 1, 33))
        let tokenId = byteVecSlice!(payload, 33, 65)
        let tokenChainId = u256From2Byte!(byteVecSlice!(payload, 65, 67))
        let recipientAddress = byteVecSlice!(payload, 67, 99)
        let recipientChainId = u256From2Byte!(byteVecSlice!(payload, 99, 101))
        assert!(recipientChainId == localChainId)
        let arbiterFee = u256From32Byte!(byteVecSlice!(payload, 101, 133))
        assert!(size!(payload) == 133)
        return tokenId, tokenChainId, recipientAddress, amount, arbiterFee
    }
}
