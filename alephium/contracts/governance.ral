TxContract Governance(
    chainId: U256,
    governanceChainId: U256,
    governanceEmitterAddress: ByteVec,
    mut receivedSequence: U256,
    mut messageFee: U256,
    mut guardianSets: [ByteVec; 2], // keep previous and current guardian set
    mut guardianSetIndexes: [U256; 2],
    mut previousGuardianSetExpirationTime: U256
) {

    event WormholeMessage(sender: ByteVec, targetChainId: U256, sequence: U256, nonce: ByteVec, payload: ByteVec, consistencyLevel: U256)

    pub fn getMessageFee() -> (U256) {
        return messageFee
    }

    @using(preapprovedAssets = true, assetsInContract = true)
    pub fn publishWormholeMessage(
        payer: Address,
        targetChainId: U256,
        sequence: U256,
        nonce: ByteVec,
        payload: ByteVec,
        consistencyLevel: U256
    ) -> () {
        transferAlphToSelf!(payer, messageFee)
        emit WormholeMessage(callerContractId!(), targetChainId, sequence, nonce, payload, consistencyLevel)
    }

    // return emitterChainId, targetChainId, emitterAddress, sequence, payload
    fn parseBody(body: ByteVec) -> (U256, U256, ByteVec, U256, ByteVec) {
        let timestamp = u256From4Byte!(byteVecSlice!(body, 0, 4))
        let nonce = u256From4Byte!(byteVecSlice!(body, 4, 8))
        let emitterChainId = u256From2Byte!(byteVecSlice!(body, 8, 10))
        let targetChainId = u256From2Byte!(byteVecSlice!(body, 10, 12))
        let emitterAddress = byteVecSlice!(body, 12, 44)
        let sequence = u256From8Byte!(byteVecSlice!(body, 44, 52))
        let consistencyLevel = u256From1Byte!(byteVecSlice!(body, 52, 53))
        let payload = byteVecSlice!(body, 53, size!(body))
        return emitterChainId, targetChainId, emitterAddress, sequence, payload
    }

    pub fn parseAndVerifyGovernanceVAAGeneric(vaa: ByteVec, targetSequence: U256, coreModule: U256, action: ByteVec) -> (U256, ByteVec) {
        let body = parseAndVerifySignatures(vaa, true)
        let (emitterChainId, targetChainId, emitterAddress, msgSequence, payload) = parseBody(body)
        assert!(emitterChainId == governanceChainId)
        assert!(emitterAddress == governanceEmitterAddress)
        assert!(msgSequence == targetSequence)

        // check module and governance action
        assert!(u256From32Byte!(byteVecSlice!(payload, 0, 32)) == coreModule)
        assert!(byteVecSlice!(payload, 32, 33) == action)

        return targetChainId, payload
    }

    fn parseAndVerifyGovernanceVAA(vaa: ByteVec, action: ByteVec) -> (U256, ByteVec) {
        let coreModule = 0x436f7265 // module identifier: "Core"
        let (targetChainId, payload) = parseAndVerifyGovernanceVAAGeneric(vaa, receivedSequence, coreModule, action)
        receivedSequence = receivedSequence + 1
        return targetChainId, payload
    }

    pub fn parseAndVerifyVAA(vaa: ByteVec) -> (U256, U256, ByteVec, U256, ByteVec) {
        let body = parseAndVerifySignatures(vaa, false)
        return parseBody(body)
    }

    // parse VAA and verify signatures, return body
    fn parseAndVerifySignatures(data: ByteVec, useLatestGuardianSetIndex: Bool) -> (ByteVec) {
        assert!(byteVecSlice!(data, 0, 1) == #01)
        let guardianSetIndex = u256From4Byte!(byteVecSlice!(data, 1, 5))
        if (useLatestGuardianSetIndex) {
            assert!(guardianSetIndex == guardianSetIndexes[1])
        }

        let signatureSize = u256From1Byte!(byteVecSlice!(data, 5, 6))
        let guardians = getGuardiansInfo(guardianSetIndex)
        let guardianSize = u256From1Byte!(byteVecSlice!(guardians, 0, 1))

        // check signature quorum size
        assert!(guardianSize != 0) // this is guaranteed, but let's double check
        let quorumSize = ((guardianSize * 2) / 3) + 1
        assert!(quorumSize <= signatureSize)

        let body = byteVecSlice!(data, 6 + signatureSize * 66, size!(data))
        let hash = keccak256!(keccak256!(body))

        let mut offset = 6
        let mut lastGuardianIndex = -1
        let mut sigIndex = 0
        while (sigIndex < signatureSize) {
            // 1 byte `guardianIndex` and 65 bytes signature
            let guardianIndex = u256From1Byte!(byteVecSlice!(data, offset, offset + 1))

            let guardianIndexI256 = i256!(guardianIndex)
            assert!(guardianIndexI256 > lastGuardianIndex)
            lastGuardianIndex = guardianIndexI256

            let signature = byteVecSlice!(data, offset + 1, offset + 66)
            let recId = u256From1Byte!(byteVecSlice!(signature, 64, 65)) + 27
            let newSignature = byteVecSlice!(signature, 0, 64) ++ u256To1Byte!(recId)
            let guardianKeyIndex = 1 + guardianIndex * 20
            let guardianKey = byteVecSlice!(guardians, guardianKeyIndex, guardianKeyIndex + 20)
            assert!(guardianKey == ethEcRecover!(hash, newSignature))

            offset = offset + 66
            sigIndex = sigIndex + 1
        }
        return body
    }

    fn getGuardiansInfo(guardianSetIndex: U256) -> ByteVec {
        if (guardianSetIndex == guardianSetIndexes[1]) {
            return guardianSets[1]
        }
        if (guardianSetIndex == guardianSetIndexes[0]) {
            assert!(blockTimeStamp!() <= previousGuardianSetExpirationTime)
            return guardianSets[0]
        }
        assert!(false) // invalid guardian set index
        return #
    }

    pub fn submitContractUpgrade(vaa: ByteVec) -> () {
        let (targetChainId, payload) = parseAndVerifyGovernanceVAA(vaa, #01)
        assert!(targetChainId == chainId)
        let contractCodeLength = u256From2Byte!(byteVecSlice!(payload, 33, 35))
        let mut offset = 35 + contractCodeLength
        let newContractCode = byteVecSlice!(payload, 35, offset)
        let payloadLength = size!(payload)
        if (payloadLength == offset) {
            migrate!(newContractCode)
        } else {
            let prevStateHash = byteVecSlice!(payload, offset, offset + 32)
            offset = offset + 32
            let currentEncodedState = u256To32Byte!(receivedSequence)
                ++ u256To32Byte!(messageFee)
                ++ u256To32Byte!(guardianSetIndexes[0])
                ++ u256To32Byte!(guardianSetIndexes[1])
            assert!(prevStateHash == blake2b!(currentEncodedState))
            let stateLength = u256From2Byte!(byteVecSlice!(payload, offset, offset + 2))
            offset = offset + 2
            let newContractState = byteVecSlice!(payload, offset, offset + stateLength)
            offset = offset + stateLength
            assert!(payloadLength == offset)
            migrateWithFields!(newContractCode, newContractState)
        }
    }

    fn updatePreviousGuardianSet() -> () {
        guardianSets[0] = guardianSets[1]
        guardianSetIndexes[0] = guardianSetIndexes[1]
        previousGuardianSetExpirationTime = blockTimeStamp!() + 86400000 // expired after one day
    }

    pub fn submitNewGuardianSet(vaa: ByteVec) -> () {
        updatePreviousGuardianSet()
        let (targetChainId, payload) = parseAndVerifyGovernanceVAA(vaa, #02)
        assert!(targetChainId == chainId || targetChainId == 0)
        let newGuardianSetIndex = u256From4Byte!(byteVecSlice!(payload, 33, 37))
        assert!(newGuardianSetIndex == guardianSetIndexes[1] + 1)
        let newGuardianSetSize = u256From1Byte!(byteVecSlice!(payload, 37, 38))
        assert!(newGuardianSetSize > 0)
        let payloadSize = 38 + newGuardianSetSize * 20
        assert!(size!(payload) == payloadSize)
        guardianSetIndexes[1] = newGuardianSetIndex
        guardianSets[1] = byteVecSlice!(payload, 37, payloadSize)
    }

    pub fn submitSetMessageFee(vaa: ByteVec) -> () {
        let (targetChainId, payload) = parseAndVerifyGovernanceVAA(vaa, #03)
        assert!(targetChainId == chainId)
        let fee = u256From32Byte!(byteVecSlice!(payload, 33, 65))
        assert!(size!(payload) == 65)
        messageFee = fee
    }

    @using(assetsInContract = true)
    pub fn submitTransferFees(vaa: ByteVec) -> () {
        let (targetChainId, payload) = parseAndVerifyGovernanceVAA(vaa, #04)
        assert!(targetChainId == chainId || targetChainId == 0)
        let amount = u256From32Byte!(byteVecSlice!(payload, 33, 65))
        let recipient = byteVecSlice!(payload, 65, 97)
        assert!(size!(payload) == 97)
        transferAlphFromSelf!(byteVecToAddress!(#00 ++ recipient), amount)
    }
}
