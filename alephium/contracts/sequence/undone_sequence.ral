import "../constants.ral"

TxContract UndoneSequence(
    parentId: ByteVec,
    begin: U256,
    mut sequences: U256,
    refundAddress: Address
) extends Constants() {
    @using(assetsInContract = true)
    pub fn checkUndone(seq: U256) -> () {
        assertWithErrorCode!(callerContractId!() == parentId, ErrorCodes.InvalidCaller)
        assertWithErrorCode!(seq >= begin, ErrorCodes.InvalidSequence)
        let offset = seq - begin
        assertWithErrorCode!(offset < 256, ErrorCodes.InvalidSequence)
        let flag = 1 << offset
        assertWithErrorCode!(sequences & flag == 0, ErrorCodes.InvalidSequence)
        sequences = sequences | flag
        if (sequences == SequenceExecutedFlag) {
            destroySelf!(refundAddress)
        }
    }

    @using(assetsInContract = true)
    pub fn destroy() -> () {
        assertWithErrorCode!(callerContractId!() == parentId, ErrorCodes.InvalidCaller)
        destroySelf!(refundAddress)
    }
}
