import "event_emitter.ral"

TxContract TokenWrapperFactory<tokenWrapperByteCode: ByteVec, eventEmitterId: ByteVec>() {
    pub payable fn createWrapper(
        tokenBridgeId: ByteVec,
        localChainId: U256,
        remoteChainId: U256,
        tokenContractId: ByteVec,
        isLocalToken: Bool,
        tokenDecimals: U256,
        tokenSymbol: ByteVec,
        tokenName: ByteVec,
        payer: Address,
        createContractAlphAmount: U256
    ) -> () {
        let contractCode = tokenWrapperByteCode
        let tokenBridgeForChainId = callerContractId!()
        let encodedState = encodeToByteVec!(
            tokenBridgeId,
            tokenBridgeForChainId,
            localChainId,
            remoteChainId,
            tokenContractId,
            isLocalToken,
            tokenDecimals,
            tokenSymbol,
            tokenName
        )

        approveAlph!(payer, createContractAlphAmount)
        assert!(remoteChainId != localChainId)
        let mut contractId = #
        if (isLocalToken) {
            contractId = createContract!(contractCode, encodedState)
        } else {
            let maxAmount = 0xffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff
            contractId = createContractWithToken!(contractCode, encodedState, maxAmount)
        }

        let eventEmitter = EventEmitter(eventEmitterId)
        eventEmitter.publishTokenWrapperCreated(tokenBridgeForChainId, contractId, isLocalToken, tokenContractId, remoteChainId)
    }
}
