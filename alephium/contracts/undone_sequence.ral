TxContract UndoneSequence<undoneSequenceMaxSize: U256, undoneSequenceMaxDistance: U256>(
    owner: ByteVec,
    mut undone: ByteVec
) {
    pub fn getOwner() -> ByteVec {
        return owner
    }

    pub fn getUndoneList() -> ByteVec {
        return undone
    }

    fn add(seq: U256) -> ByteVec {
        if (undone == #) {
            undone = u256To8Byte!(seq)
            return #
        }

        let mut length = size!(undone)
        let max = u256From8Byte!(byteVecSlice!(undone, length - 8, length))
        assert!(seq > max)

        undone = undone ++ u256To8Byte!(seq)
        length = length + 8
        if ((length / 8) > undoneSequenceMaxSize) {
            let removed = byteVecSlice!(undone, 0, 8)
            undone = byteVecSlice!(undone, 8, length)
            return removed
        }
        return #
    }

    fn addSequences(seq: U256, bitMap: U256) -> ByteVec {
        let mut index = 0
        let mut removed = #
        while (index < 8) {
            if ((bitMap >> index) & 0x01 == 0) {
                removed = removed ++ add(seq + index)
            }
            index = index + 1
        }
        return removed
    }

    pub fn addToUndone(seq: U256, current: U256, bitMap: U256) -> ByteVec {
        assert!(owner == callerContractId!())
        let mut index = 0
        let mut removed = #
        while (index < 256) {
            let value = (bitMap >> index) & 0xff
            if (value != 0xff) {
                removed = removed ++ addSequences(seq + index, value)
            }
            index = index + 8
        }
        return removed ++ removeOldUndone(current)
    }

    fn removeOldUndone(current: U256) -> ByteVec {
        if (undone == #) {
            return #
        }

        let mut min = u256From8Byte!(byteVecSlice!(undone, 0, 8))
        let mut removed = #
        while (current > min + undoneSequenceMaxDistance) {
            removed = removed ++ byteVecSlice!(undone, 0, 8)
            undone = byteVecSlice!(undone, 8, size!(undone))
            if undone != # {
                min = u256From8Byte!(byteVecSlice!(undone, 0, 8))
            } else {
                return removed
            }
        }
        return removed
    }

    pub fn trySetDone(seq: U256) -> Bool {
        assert!(owner == callerContractId!())
        let mut begin = 0i
        let mut end = i256!(size!(undone) / 8) - 1i
        while (begin <= end) {
            let mid = (begin + end) / 2i
            let offset = u256!(mid * 8i)
            let midSeq = u256From8Byte!(byteVecSlice!(undone, offset, offset + 8))

            if (seq == midSeq) {
                undone = byteVecSlice!(undone, 0, offset) ++ byteVecSlice!(undone, offset + 8, size!(undone))
                return true
            }

            if (seq > midSeq) {
                begin = mid + 1i
            } else {
                end = mid - 1i
            }
        }
        return false
    }
}
